/-
Copyright (c) 2025-2026 Yijun Yuan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yijun Yuan
-/
import Mathlib.SetTheory.Cardinal.Aleph
import Mathlib.Order.Closure

/-!
This file provides a small amount of order-theoretic infrastructure around the Dedekind‚ÄìMacNeille
completion.

The Dedekind‚ÄìMacNeille completion of a poset `Œ±` is the complete lattice obtained by closing sets
under the Galois connection between lower and upper bounds. In this project it is used as a
convenient ambient complete lattice in which certain ‚Äúquotient‚Äù constructions can safely take the
value `‚ä§` (e.g. to model division by zero).

Main contents:
- A `CompleteLattice` instance for the type of closed sets of a closure operator.
- Definitions of the Dedekind‚ÄìMacNeille Galois connection, the induced closure operator, and the
  completion.
- Order structure instances for the completion when `Œ±` is linearly ordered.
- An order embedding `coe' : Œ± ‚Ü™o DedekindMacNeilleCompletion Œ±` and a coercion `Coe`.
- A universal property statement `univ_prop_DedekindMacNeilleCompletion` for extending order
  embeddings.

This file lives in the `OrderTheory` namespace because it provides general tools independent of the
  specific Harder‚ÄìNarasimhan game development.
-/

namespace OrderTheory

/-
The closed sets of a closure operator form a complete lattice.

Mathematically, if `T` is a closure operator on `Set Œ±`, then its fixed points (closed sets) are
closed under bitrary intersections and under closure of unions, which yields `sInf`/`sSup`.

API note: we provide the full `CompleteLattice` instance for `ClosureOperator.Closeds T`, with order
given by subset inclusion on underlying sets.
-/
instance {Œ± : Type*} [PartialOrder Œ±] (T : ClosureOperator (Set Œ±)) :
CompleteLattice (ClosureOperator.Closeds T) where
  top :=‚ü®Set.univ,ClosureOperator.isClosed_iff_closure_le.mpr fun ‚¶Éa‚¶Ñ a ‚Ü¶ trivial‚ü©
  le_top A := fun ‚¶Éa‚¶Ñ a ‚Ü¶ trivial
  bot := ‚ü®T ‚àÖ, ClosureOperator.isClosed_closure T ‚àÖ‚ü©
  bot_le A := by
    intro a ha
    simp only at *
    exact (ClosureOperator.IsClosed.closure_eq A.property) ‚ñ∏
      (T.monotone <| Set.empty_subset A.val) ha
  inf A B := ‚ü®A.val ‚à© B.val,ClosureOperator.isClosed_iff_closure_le.mpr fun x hx ‚Ü¶
    (ClosureOperator.IsClosed.closure_eq A.property) ‚ñ∏
    (ClosureOperator.IsClosed.closure_eq B.property) ‚ñ∏
    ‚ü®(T.monotone <| Set.inter_subset_left) hx,(T.monotone <| Set.inter_subset_right) hx‚ü©‚ü©
  le_inf A B C h1 h2 := fun a ha ‚Ü¶ ‚ü®h1 ha,h2 ha‚ü©
  inf_le_left A B := fun ‚¶Éa‚¶Ñ b ‚Ü¶ b.1
  inf_le_right A B := fun ‚¶Éa‚¶Ñ b ‚Ü¶ b.right
  sup A B := ‚ü®T (A.val ‚à™ B.val), ClosureOperator.isClosed_closure T (A.val ‚à™ B.val)‚ü©
  sup_le A B C h1 h2 := by
    intro a ha
    simp only at *
    exact (ClosureOperator.IsClosed.closure_eq C.property) ‚ñ∏
      (T.monotone <| Set.union_subset h1 h2) ha
  le_sup_left := by
    intro A B
    nth_rw 1 [Subtype.coe_eq_of_eq_mk (ClosureOperator.IsClosed.closure_eq A.property).symm]
    exact ClosureOperator.monotone T Set.subset_union_left
  le_sup_right := by
    intro A B
    nth_rw 1 [Subtype.coe_eq_of_eq_mk (ClosureOperator.IsClosed.closure_eq B.property).symm]
    exact ClosureOperator.monotone T Set.subset_union_right
  sInf ùíÆ := by
    refine ‚ü®‚ãÇ a ‚àà ùíÆ, a.val,ClosureOperator.isClosed_iff_closure_le.mpr fun x hx ‚Ü¶ ?_‚ü©
    simp only [Set.mem_iInter, Subtype.forall] at *
    refine fun S hS hSb ‚Ü¶ (ClosureOperator.IsClosed.closure_eq hS) ‚ñ∏ T.monotone (fun x hx ‚Ü¶ ?_) hx
    simp only [Set.mem_iInter, Subtype.forall] at hx
    exact hx S hS hSb
  le_sInf ùíÆ A hA := by
    intro x h
    simp only [Subtype.forall, Set.mem_iInter] at *
    exact fun S hS hSb ‚Ü¶ hA S hS hSb h
  sInf_le ùíÆ A:= by
    intro hA x hx
    simp only [Set.mem_iInter, Subtype.forall] at *
    exact hx A.val A.prop hA
  sSup ùíÆ := ‚ü®T (‚ãÉ a ‚àà ùíÆ, a.val),ClosureOperator.isClosed_closure T (‚ãÉ a ‚àà ùíÆ, a.val)‚ü©
  le_sSup ùíÆ A hA:= fun x hx ‚Ü¶ ClosureOperator.monotone T (Set.subset_biUnion_of_mem hA) <|
    (ClosureOperator.IsClosed.closure_eq A.property).symm ‚ñ∏ hx
  sSup_le ùíÆ A hA := by
    intro x hx
    simp only
    refine (ClosureOperator.IsClosed.closure_eq A.property) ‚ñ∏
      ClosureOperator.monotone T (fun y hy ‚Ü¶ ?_) hx
    simp only [Set.mem_iUnion, exists_prop, Subtype.exists, exists_and_right] at hy
    exact Exists.casesOn hy
      fun S h ‚Ü¶ And.casesOn h fun left hSb ‚Ü¶ Exists.casesOn left fun hS hP ‚Ü¶ hA ‚ü®S, hS‚ü© hP hSb


section DedekindMacNeille

/-
The Dedekind‚ÄìMacNeille Galois connection.

The left adjoint sends a set `A` to its set of upper bounds (viewed in the order dual), and the
right adjoint sends a set of upper bounds back to the set of lower bounds.

This Galois connection is the standard starting point for defining the Dedekind‚ÄìMacNeille closure.
-/
lemma DedekindMacNeilleConnection (Œ± : Type*) [PartialOrder Œ±] :
GaloisConnection (fun A ‚Ü¶ (OrderDual.toDual (upperBounds A)))
(fun A : (Set Œ±)·µí·µà ‚Ü¶ lowerBounds A.ofDual) :=
fun _ _ ‚Ü¶ ‚ü®fun h _ ha ‚¶É_‚¶Ñ a_3 ‚Ü¶ h a_3 ha, fun h _ ha ‚¶É_‚¶Ñ a_2 ‚Ü¶ h a_2 ha‚ü©


/-
The Dedekind‚ÄìMacNeille closure operator on `Set Œ±`.

It is obtained from the Galois connection by the standard `GaloisConnection.closureOperator`
construction.
-/
def DedekindMacNeilleClosureOperator (Œ± : Type*) [PartialOrder Œ±] :
ClosureOperator (Set Œ±) := GaloisConnection.closureOperator <| DedekindMacNeilleConnection Œ±


/-
The Dedekind‚ÄìMacNeille completion of `Œ±`.

This is defined as the type of closed sets for the Dedekind‚ÄìMacNeille closure operator.
It carries a complete lattice structure by the general instance above.
-/
abbrev DedekindMacNeilleCompletion (Œ± : Type*) [PartialOrder Œ±] :=
(DedekindMacNeilleClosureOperator Œ±).Closeds


/-
The Dedekind‚ÄìMacNeille completion is a complete lattice.

This is obtained by typeclass inference from the `Closeds` complete lattice instance.
-/
instance {Œ± : Type*} [PartialOrder Œ±] : CompleteLattice (DedekindMacNeilleCompletion Œ±) :=
inferInstance

/-
If `Œ±` is a linear order, then the completion is totally ordered.

API note: we provide `Std.Total` for `‚â§`, which is then used to build a `LinearOrder` instance.
-/
instance {Œ± : Type*} [LinearOrder Œ±] :
@Std.Total (DedekindMacNeilleCompletion Œ±) instCompleteLatticeDedekindMacNeilleCompletion.le := by
  refine { total := ?_ }
  intro a b
  rcases a with ‚ü®A, hA‚ü©
  rcases b with ‚ü®B, hB‚ü©
  simp only [Subtype.mk_le_mk, Set.le_eq_subset]
  apply or_iff_not_imp_left.2
  intro h1
  rcases Set.not_subset_iff_exists_mem_notMem.1 h1 with ‚ü®a‚ÇÄ,ha‚ÇÄ‚ü©
  intro b hb
  rw [‚Üê hB] at hb
  simp only [GaloisConnection.lowerAdjoint_toFun, OrderDual.ofDual_toDual] at hb
  by_contra hc
  rw [‚Üê hA] at hc
  simp only [GaloisConnection.lowerAdjoint_toFun, OrderDual.ofDual_toDual] at hc
  unfold lowerBounds at hc
  simp only [Set.mem_setOf_eq, not_forall,  not_le] at hc
  rcases hc with ‚ü®a',ha'1,ha'2‚ü©
  have hhb : b ‚àà upperBounds A := upperBounds_mono (fun ‚¶Éa‚¶Ñ a ‚Ü¶ a) (le_of_lt ha'2) ha'1
  have hB : B = lowerBounds (upperBounds B) := by
    nth_rw 1 [‚Üê hB]
    simp only [GaloisConnection.lowerAdjoint_toFun, OrderDual.ofDual_toDual]
  exact ha‚ÇÄ.2 <| hB ‚ñ∏ lowerBounds_mono (fun ‚¶Éa‚¶Ñ a ‚Ü¶ a) (hhb ha‚ÇÄ.1) hb

--open Classical

/-
If `Œ±` is linearly ordered, then the completion is linearly ordered.

This instance is noncomputable because we use classical decidability for `‚â§`.
The order is the inherited one from the complete lattice structure.
-/
noncomputable instance {Œ± : Type*} [LinearOrder Œ±] :
LinearOrder (DedekindMacNeilleCompletion Œ±) := {
  instCompleteLatticeDedekindMacNeilleCompletion with
  le_total := by exact fun a b ‚Ü¶ Std.le_total
  toDecidableLE := Classical.decRel LE.le
  min_def a b := by
    by_cases h : a ‚â§ b
    ¬∑ simp only [h, inf_of_le_left, ‚ÜìreduceIte]
    ¬∑ simp only [h, ‚ÜìreduceIte, inf_eq_right]
      simpa [h] using (Std.le_of_not_ge h)
  max_def a b := by
    by_cases h : a ‚â§ b
    ¬∑ simp only [h, sup_of_le_right, ‚ÜìreduceIte]
    ¬∑ simp only [h, ‚ÜìreduceIte, sup_eq_left]
      simpa only [h, false_or] using (Std.le_of_not_ge h)
  }

/-
If `Œ±` is linearly ordered, the completion is a complete linear order.

API note: this bundles `CompleteLattice` with `LinearOrder`.
-/
noncomputable instance {Œ± : Type*} [LinearOrder Œ±] :
CompleteLinearOrder (DedekindMacNeilleCompletion Œ±) :=
  {instLinearOrderDedekindMacNeilleCompletion, LinearOrder.toBiheytingAlgebra
    (DedekindMacNeilleCompletion Œ±), instCompleteLatticeDedekindMacNeilleCompletion with}


/-
Order embedding of `Œ±` into its Dedekind‚ÄìMacNeille completion.

The map sends `x : Œ±` to the principal down-set `Set.Iic x`, which is closed under the
Dedekind‚ÄìMacNeille closure. Order preservation is `Set.Iic_subset_Iic`.

API note: this is provided as an `OrderEmbedding` so that monotonicity is built in.
-/
def coe' {Œ± : Type*} [PartialOrder Œ±] : Œ± ‚Ü™o DedekindMacNeilleCompletion Œ± := by
  have inj: ‚àÄ x : Œ±, (DedekindMacNeilleClosureOperator Œ±).IsClosed (Set.Iic x) :=
    fun x ‚Ü¶ Set.ext fun y ‚Ü¶ ‚ü®fun hy ‚Ü¶ hy (by simp only [upperBounds,
    GaloisConnection.lowerAdjoint_toFun, Set.mem_Iic, OrderDual.ofDual_toDual, Set.mem_setOf_eq,
    imp_self, implies_true]),fun hy x_1 ha ‚Ü¶ ha hy‚ü©
  have : Function.Injective fun x ‚Ü¶ (‚ü®Set.Iic x,inj x‚ü© : DedekindMacNeilleCompletion Œ±) := by
    intro a b hab
    simp only [Subtype.mk.injEq] at hab
    exact le_antisymm (hab ‚ñ∏ Set.self_mem_Iic).out (hab.symm ‚ñ∏ Set.self_mem_Iic).out
  use ‚ü®fun x ‚Ü¶ ‚ü®Set.Iic x, inj x‚ü©,this‚ü©
  simp only [Function.Embedding.coeFn_mk, Subtype.mk_le_mk, Set.le_eq_subset, Set.Iic_subset_Iic,
    implies_true]


/-
Coercion from `Œ±` to its Dedekind‚ÄìMacNeille completion.

This allows writing `((x : Œ±) : DedekindMacNeilleCompletion Œ±)`.
-/
instance {Œ± : Type*} [PartialOrder Œ±] : Coe Œ± (DedekindMacNeilleCompletion Œ±) := ‚ü®coe'.toFun‚ü©


/-
Universal property (extension) for the Dedekind‚ÄìMacNeille completion.

Given an order embedding `f : Œ± ‚Ü™o Œ≤` into a complete lattice `Œ≤`, this theorem produces an order
embedding `f' : DedekindMacNeilleCompletion Œ± ‚Ü™o Œ≤` such that `f = f' ‚àò coe'`.

API note: the constructed `f'` is defined by a `sSup` over lower bounds of upper bounds of the image
of `x`. The statement is used in the project to justify viewing the completion as a canonical
completion object.
-/
theorem univ_prop_DedekindMacNeilleCompletion
{Œ± : Type*} [PartialOrder Œ±] {Œ≤ : Type*} [CompleteLattice Œ≤] (f : Œ± ‚Ü™o Œ≤) :
‚àÉ f' : DedekindMacNeilleCompletion Œ± ‚Ü™o Œ≤, f = f' ‚àò coe' := by
  let g := fun x : DedekindMacNeilleCompletion Œ± ‚Ü¶ sSup <| lowerBounds <| upperBounds <| f '' x.val
  have : ‚àÄ (A B : DedekindMacNeilleCompletion Œ±), g A ‚â§ g B ‚Üî A ‚â§ B := by
    refine fun A B ‚Ü¶ ‚ü®?_,?_‚ü©
    ¬∑ intro h
      by_contra!
      rcases (Set.not_subset.1 this) with ‚ü®a, haA, haB‚ü©
      have : ‚àÉ u ‚àà upperBounds B, ¬¨ a ‚â§ u := by
        by_contra!
        exact haB ((ClosureOperator.IsClosed.closure_eq B.property) ‚ñ∏ this)
      refine (fun w ‚Ü¶ this.choose_spec.2 (f.map_rel_iff'.1 w)) ?_
      have h‚ÇÅ : f a ‚â§ g A := le_sSup fun u hu ‚Ü¶ hu (Exists.intro a ‚ü®haA, rfl‚ü©)
      have h‚ÇÇ : g B ‚â§ f (this.choose) := by
        refine sSup_le fun y hy ‚Ü¶ hy ?_
        simp only [upperBounds, Set.mem_image, forall_exists_index, and_imp,
          forall_apply_eq_imp_iff‚ÇÇ, Set.mem_setOf_eq, OrderEmbedding.le_iff_le]
        exact this.choose_spec.1.out
      exact le_trans h‚ÇÅ <| le_trans h h‚ÇÇ
    ¬∑ intro h
      simp only [upperBounds, Set.mem_image, forall_exists_index, and_imp, forall_apply_eq_imp_iff‚ÇÇ,
        sSup_le_iff, g]
      exact fun y hy ‚Ü¶ hy.out fun w hw ‚Ü¶ le_sSup fun ‚¶Éa‚¶Ñ a ‚Ü¶ a w (h hw)
  refine ‚ü®‚ü®‚ü®g,fun x y h ‚Ü¶ le_antisymm ((this x y).1 <| (le_antisymm_iff.1 h).1)
    ((this y x).1 <| (le_antisymm_iff.1 h).2)‚ü©,?_‚ü©,?_‚ü©
  ¬∑ simp only [Function.Embedding.coeFn_mk, Subtype.forall, Subtype.mk_le_mk, Set.le_eq_subset, g]
    exact fun x hx y hy ‚Ü¶ this ‚ü®x, hx‚ü© ‚ü®y, hy‚ü©
  ¬∑ refine funext fun x ‚Ü¶ ?_
    simp only [RelEmbedding.coe_mk, Function.Embedding.coeFn_mk, coe', Function.comp_apply, g]
    refine le_antisymm (le_sSup fun a ha ‚Ü¶ ha.out <|
      Set.mem_image_of_mem f Set.self_mem_Iic) <| sSup_le fun _ hb ‚Ü¶ hb ?_
    simp only [upperBounds, Set.mem_image, Set.mem_Iic, forall_exists_index, and_imp,
      forall_apply_eq_imp_iff‚ÇÇ, Set.mem_setOf_eq, OrderEmbedding.le_iff_le, imp_self, implies_true]

--TODO: joint-dense, meet-dense
end DedekindMacNeille

end OrderTheory
